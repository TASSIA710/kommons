plugins {
	id 'org.jetbrains.kotlin.multiplatform' version '1.5.21' apply false
	id 'org.jetbrains.kotlin.plugin.serialization' version '1.5.21' apply false
}

allprojects {

	if (project.name == 'kommons') {
		return
	}

	apply plugin: 'maven-publish'
	apply plugin: 'org.jetbrains.kotlin.multiplatform'

	group = 'net.tassia.kommons'
	version = '1.0.0-PRC-1'

	repositories {
		mavenCentral()
	}

	kotlin {

		jvm()

		sourceSets {
			commonTest {
				dependencies {
					implementation kotlin('test-common')
					implementation kotlin('test-annotations-common')
				}
			}
			jvmTest {
				dependencies {
					implementation kotlin('test')
				}
			}
		}

	}

	publishing {
		repositories {
			maven {
				url = uri('https://nexus.tassia.net/repository/maven-tassia-kommons/')
				credentials {
					username = findProperty("PUBLISH_MAVEN_USERNAME") ?: "N/A"
					password = findProperty("PUBLISH_MAVEN_PASSWORD") ?: "N/A"
				}
			}
		}
	}

}
